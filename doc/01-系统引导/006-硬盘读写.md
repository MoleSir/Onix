# 硬盘读写

## 硬盘结构

- 扇区：是硬盘读写的最小单位。最小一次度写一个扇区，最多 256 个扇区；
- 机器臂的寻道时间是硬盘性能的主要瓶颈；
- 一般情况下，一个磁道有 63 个扇区，主要是由于 BIOS 最大支持；
- 磁道从外侧计数，所以一般情况下 C 盘的读写度数最快；

## IDE / ATA PIO Mode

Port Input Output 端口输入输出模式。

端口是外部设备内部的寄存器。

- IDE: Integrated Drive Electonics 集成电子驱动器
- ATA: Advanced Technology Attachment
- SATA / PATA

## 硬盘读写

- CHS 模式，Cylinder / Head / Sector，即：柱面、磁头、磁道三个坐标确定一个位置；
- LBA 模式，Logical Block Address，把磁盘抽象为一个文件，指定要读写的块即可；
    - LBA28 模式，总共能访问 128G 的磁盘空间；

硬盘控制端口：

| Primary 通道 | Secondary 通道 | in 操作 | out 操作 |
| ---- | ---- | ---- | ---- |
| 0x1F0 | 0x170 | Data | Data|
| 0x1F1 | 0x171 | Error | Features|
| 0x1F2 | 0x172 | Sector count | Sector count|
| 0x1F3 | 0x173 | LBA low | LBA low|
| 0x1F4 | 0x174 | LBA mid | LBA mid|
| 0x1F4 | 0x174 | LBA high | LBA higt|
| 0x1F5 | 0x175 | Device | Device|
| 0x1F6 | 0x176 | Status | Command|

这里只使用 Primary 通道，即主通道：

- 0x1F0：16bit 端口，用于读写数据；
- 0x1F1: 检测前一个指令的错误；
- 0x1F2: 读写扇区的数量；
- 0x1F3: 起始扇区的 0 ~ 7 位；
- 0x1F4：起始扇区的 8 ~ 15 位；
- 0x1F5: 起始扇区的 16 ~ 23 位；
- 0x1F6:
    - 0 ~ 3 位：起始扇区的 24 ~ 27 位；
    - 4 位：0 表示主盘、1 表示从盘；
    - 6 位：0 表示 CHS 模式，1 表示 LBA 模式；
    - 5、7 位：固定为 1；
- 0x1F7: out 操作（作为输出端口）
    - 0xEC: 识别硬盘；
    - 0x20: 读硬盘；
    - 0x30: 写硬盘；
- 0x1F7: in 操作（作为输入端口），如果下列各位为 1
    - 0 位：ERR 错误；
    - 3 位：DRQ 数据准备完毕；
    - 7 位：BSY 硬盘繁忙；

可以根据端口的说明对磁盘进行读写操作。

> 端口是对设备的抽象，设备的生产产商提供手册供程序员参考。

## 读写硬盘程序

操作系统提供 `out` 与 `int` 指令对各种端口进行配置或读取；

这个两个指令只能对 `al` 与 `dx` 寄存器进行，例如：

- `out dx, al`：对 `dx` 寄存器的值对应的端口写入 `al` 寄存器的值。例如令 `dx` 为 0x1f0 就可以把 `al` 写入到磁盘了；
- `in ax, dx`：向 `ax` 寄存器写入：`dx` 寄存器的值对应的端口的值（有点绕，可以看后面的程序）。例如令 `dx` 为 0x1f0 就可以把磁盘的数据读入 `al` 了；

### 读硬盘

对应上述对端口的描述，定义 `read_disk` 函数，进行写硬盘：

#### 配置端口

进入函数之前，把需要读扇区的数量写入 `bl` 寄存器，起始扇区序号写入 `cl` 寄存器。

````assembly
read_disk:
    ; 设置读扇区数量
    mov dx, 0x1f2; 把 0x1f2 写入 dx
    mov al, bl; 把 
    out dx, al; 把 bl 的值写入端口 0x1f2，即配置扇区个数

    ; 设置起始扇区序号，由于序号可以有 24 位（端口 0x1f3 - 0x1f4），但只能用 8 位的寄存器 al 写，所以要写三次
    inc dx; 0x1f3
    mov al, cl; 起始扇区的前 8 位
    out dx, al;

    inc dx; 0x1f4
    shr ecx, 8
    mov al, cl; 起始扇区的中 8 位
    out dx, al;

    inc dx; 0x1f5
    shr ecx, 8
    mov al, cl; 起始扇区的高 8 位
    out dx, al;

    ; 配置模式，端口 0x1f6 的第 6 位高表示 LBA 模式
    inc dx; 0x1f6
    shr ecx, 8
    and cl, 0b1111;
    mov al, 0b1110_0000;
    or al, cl
    out dx, al; 主盘 - LBA 模式

    ; 配置读写，将 0x1f7 作为输出端，并且赋值 0x20 表示读硬盘
    inc dx; 0x1f7
    mov al, 0x20; 读硬盘
    out dx, al
````

#### 定义读数据标签

续 read_disk 函数：

````assembly
read_dick:

    ;.... 配置

    .read:
        push cx; 保存 cx 寄存器
        call .waits; 等待数据准备完毕
        call .reads; 读取一个扇区
        pop cx
        loop .read

    .waits:
        ; 根据 0x1f7 判断是否准备完毕
        mov dx, 0x1F7
        .check:
            in al, dx ; in 与 out 相对，获得端口信息
            jmp $+2; 什么都不做，但会消耗时钟周期
            jmp $+2
            jmp $+2
            and al, 0b10001000
            cmp al, 0b00001000
            jnz .check
        ret
    
    .reads:
        ; 从端口读数据
        mov dx, 0x1f0; 0x1f0 端口来读写数据
        mov cx, 256; 一个扇区 256 个字
        .readw:
            in ax, dx; 这句话就是读扇区
            jmp $+2; 什么都不做，但会消耗时钟周期
            jmp $+2
            jmp $+2
            mov [edi], ax
            add edi, 2
            loop .readw
        ret

    ret ; read_disk 函数返回
````

#### 调用实例

````assembly
mov edi, 0x1000; 读取的目标内存
mov ecx, 0; 起始扇区
mov bl, 1; 读取的扇区个数
; 从第0个扇区开始，读取 1 个扇区，内容放到内存的 0x1000 位置
call read_disk
````

### 写硬盘

#### 写硬盘程序

跟读的过程类型，不再赘述，给出程序：

````assembly
write_disk:
    ; 每次的 dx 对应硬盘的几个端口
    mov dx, 0x1f2
    mov al, bl
    out dx, al; out 就是向 dx 端口输入 al

    inc dx; 0x1f3
    mov al, cl; 起始扇区的前 8 位
    out dx, al;

    inc dx; 0x1f4
    shr ecx, 8
    mov al, cl; 起始扇区的中 8 位
    out dx, al;

    inc dx; 0x1f5
    shr ecx, 8
    mov al, cl; 起始扇区的高 8 位
    out dx, al;

    inc dx; 0x1f6
    shr ecx, 8
    and cl, 0b1111; 将高4位设置为 0

    mov al, 0b1110_0000;
    or al, cl
    out dx, al; 主盘 - LBA 模式

    inc dx; 0x1f7
    mov al, 0x30; 写硬盘
    out dx, al

    xor ecx, ecx; 清空 ecx
    mov cl, bl; 得到读写扇区的数量

    .write:
        push cx; 保存 cx 寄存器
        call .writes; 写数据
        call .waits; 等待硬盘繁忙
        pop cx
        loop .write

    ret


   .waits:
        ; 根据 0x1f7 判断是否准备完毕
        mov dx, 0x1F7
        .check:
            in al, dx ; in 与 out 相对，获得端口信息
            jmp $+2; 什么都不做，但会消耗时钟周期
            jmp $+2
            jmp $+2
            and al, 0b10000000
            cmp al, 0b00000000; 判断第 7 位是否为 0（不繁忙）
            jnz .check

        ret
    
    .writes:
        ; 从端口读数据
        mov dx, 0x1f0
        mov cx, 256; 一个扇区 256 个字
        .writesw:
            mov ax, [edi]; 将目标地址的内存写入 ax 
            out dx, ax
            jmp $+2; 什么都不做，但会消耗时钟周期
            jmp $+2
            jmp $+2
            add edi, 2
            loop .writesw
        ret
````

#### 调用实例

````assembly
mov edi, 0x1000; 从内存的这里写入到磁盘
mov ecx, 2; 写入到第二个扇区
mov bl, 1; 写的扇区个数
call write_disk
````